#!/usr/bin/env awk -f
#
# accessions-ttl-generator
#  a script to generate RDF-TTL from SRA_Accessions file available at ftp.ncbi.nlm.nih.gov/reports/Metadata
#  Author: Tazro Inutano Ohta inutano@gmail.com
#  License: MIT
#
# Columns of input accessions file
#  $1: SRA Accession ID
#  $2: SRA Submission ID
#  $11: SRA Experiment ID
#  $12: SRA Sample ID
#  $13: SRA Study ID
#  $18: BioSample ID
#  $19: BioProject ID
#

BEGIN {
  FS=OFS="\t"

  insdc="insdc:"
  bs="biosample:"
  bp="bioproject:"

  label=" rdfs:label "
  link=" idont:link "

  print "@prefix id: <http://identifiers.org/> ."
  print "@prefix idont: <http://identifiers.org/ontology/> ."
  print "@prefix insdc: <http://identifiers.org/insdc.sra/> ."
  print "@prefix biosample: <http://identifiers.org/biosample/> ."
  print "@prefix bioproject: <http://identifiers.org/bioproject/> ."
  print "@prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> ."
  print "@prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#> ."
  print ""
}

$0 ~ /^.RX/ {
  print insdc $2 " rdf:type insdc:Accession;"
  print label "\"" $2 "\";"
  print link insdc $1 ";"
  print link insdc $12 ";"
  print link insdc $13 ";"
  print link bs $18 ";"
  print link bp $19 "."

  print insdc $1 " rdf:type insdc:Experiment;"
  print label "\"" $1 "\";"
  print link insdc $2 ";"
  print link insdc $12 ";"
  print link insdc $13 ";"
  print link bs $18 ";"
  print link bp $19 "."

  print insdc $12 " rdf:type insdc:Sample;"
  print label "\"" $12 "\";"
  print link insdc $1 ";"
  print link insdc $2 ";"
  print link insdc $13 ";"
  print link bs $18 ";"
  print link bp $19 "."

  print insdc $13 " rdf:type insdc:Study;"
  print label "\"" $13 "\";"
  print link insdc $1 ";"
  print link insdc $2 ";"
  print link insdc $12 ";"
  print link bs $18 ";"
  print link bp $19 "."

  print bs $18 " rdf:type id:BioSample;"
  print label "\"" $18 "\";"
  print link insdc $1 ";"
  print link insdc $2 ";"
  print link insdc $12 ";"
  print link insdc $13 ";"
  print link bp $19 "."

  print bp $19 " rdf:type id:BioProject;"
  print label "\"" $19 "\";"
  print link insdc $1 ";"
  print link insdc $2 ";"
  print link insdc $12 ";"
  print link insdc $13 ";"
  print link bs $18 "."
}

$0 ~ /^.RR/ {
  print insdc $1 " rdf:type insdc:Run;"
  print label "\"" $1 "\";"
  print link insdc $2 ";"
  print link insdc $11 ";"
  print link insdc $12 ";"
  print link insdc $13 ";"
  print link bs $18 ";"
  print link bp $19 "."

  print insdc $2 link insdc $1 "."
  print insdc $11 link insdc $1 "."
  print insdc $12 link insdc $1 "."
  print insdc $13 link insdc $1 "."
  print bs $18 link insdc $1 "."
  print bp  $19 link insdc $1 "."
}
